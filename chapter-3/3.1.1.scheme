3.1.1 局所状態変数

(withdraw 25)
> 75

(withdraw 25)
> 50

(withdraw 60)
> "Insufficient funds"

(withdraw 15)
> 35

式(withdraw 25)が2度評価されて異なる値を返している。
これは手続きにとって新しい種類の振る舞い。

(define balance 100)
(define (withdraw amount)
  (if (>= balance amount)
      (begin (set! balance (- balance amount))
             balance)
      "Insufficient funds"))

(set! <name> <new-value>)

ここでは<name>はシンボルであり、<new-value>は任意の式。
set!は<name>を変更し、その値が<new-value>を評価して得られた結果にする。

begin
2つの式をifのテストが真の場合に評価されるようにする。
最初にbalanceを減らし、次にbalanceの値を返す。

(begin <exp1> <exp2> ... <expk>)

<exp1>から<expk>までの式は続けて評価され最後の式<expk>がbeginの形式全体の値として返る。

withdrawは望んだ通りに動くが、変数balanceが問題を表す。
グローバル環境に定義されているので、任意の手続きからアクセス可能。局所状態変数としたい。

(define new-withdraw
  (let ((balance 100))
    (lambda (amount)
      (if (>= balance amount)
          (begin (set! balance (- balance amount))
                 balance)
          "Insufficient funds"))))

ここで行ったのは、letを用いて初期値100に束縛されたローカル変数balanceを持つ環境を設置した。
この局所環境の中ではlambdaを用いてamountを引数に取り以前のwithdraw手続きと同様に振る舞う手続きを作成している。
この手続let式の評価の結果として返されるものはnew-withdrawであり正確にwithdrawと同じ振る舞いをするが、その変数balanceは他のどの手続からもアクセスできない。

set!をローカル変数と組み合わせることはローカルな状態を持つ計算オブジェクトを構築するのに用いる一般的なプログラミングテクニック。
残念なことにこのテクニックを用いることは深刻な問題をもたらす。
本当にnew-withdrawのような手続きを理解するには、手続き適用の新しいモデルの開発を必要とする。

(define (make-withdraw balance)
  (lambda (amount)
    (if (>= balance amount)
        (begin (set! balance (- balance amount))
               balance)
        "Insufficient funds")))

(define W1 (make-withdraw 100))
(define W2 (make-withdraw 100))

W1, W2が完全に独立したオブジェクトであり、各々が自身のローカル状態変数balanceを持つ。
ある口座からの引き出しは別の口座には影響しない。


引き出し同様に預け入れを扱うオブジェクトを作ることも出来る。

(define (make-account balance)
  (define (withdraw amount)
    (if (> balance amount)
        (begin (set! balance (- balance amount))
               balance)
        "Insufficient funds"))
  (define (deposit amount)
    (set! balance (+ balance amount))
    balance)
  (define (dispatch m)
    (cond ((eq? m 'withdraw) withdraw)
          ((eq? m 'deposit) deposit)
          (else (error "Unknown request: MAKE-ACCOUNT"
                       m))))
  dispatch)

各make-accountの呼び出しはローカル状態変数balanceを持つ環境を構築する。
この環境の中でmake-accountはbalanceにアクセスする手続きdepositとwithdrawを定義する。
また追加の手続きdispatchはメッセージを入力として取り2つのローカル手続きの内1つを返す。
dispatch手続きそれ自身が銀行口座オブジェクトを表現する値として返される。
これはmessage-passingプログラミングスタイル。
ここではそれをローカル変数を変更する能力と合わせて用いている。

(define acc (make-account 100))
((acc 'withdraw) 50)
50
((acc 'deposit) 40)
90
((acc 'withdraw) 60)
30

各accの呼び出しは局所的に定義されたdepositかwithdraw手続きを返し、指定されたamountに適用される。
make-withdrawを用いる場合でしたので、別の呼び出し

(define acc2 (make-account) 100)

は完全に分離された口座オブジェクトを生成し、それ自身のローカルなbalanceを持つ。
