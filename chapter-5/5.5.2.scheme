5.5.2 式のコンパイル

リンクコードのコンパイル
一般的に、各コード生成器の出力は手続きcompile-linkageにより生成された、要求されたリンク記述子を実装した命令で終わる。
もしリンク記述子がreturnなら、命令(goto (reg continue))を生成しなければならない。
これはcontinueレジスタを必要とし、他のレジスタを変更はしない。
もしリンク記述子がnextなら、なんの追加の命令も必要ない。
さもなければ、リンク記述子はラベルであり、そのラベルへのgotoを生成する。
この命令はレジスタを必要とせず、変更もしない。

(define (compile-linkage) linkage
  (cond ((eq? linkage 'return)
         (make-instruction-sequence '(continue) '()
                                    '((goto (reg continue)))))
        ((eq? linkage 'next)
         (empty-instruction-sequence))
        (else
         (make-instruction-sequence '() '()
                                    `((goto (label ,linkage)))))))

リンクのコードが命令列に対しpreservingによりcontinueレジスタを維持しながら追加される。
リンク記述子returnがcontinueレジスタを必要とするため。
もし与えられた命令列がcontinueを変更し、リンクのコードがそれを必要とする場合、continueは保存と復元が行われる。
(define (end-with-linkage linkage instruction-sequence)
  (preserving '(continue)
              instruction-sequence
              (compile-linkage linkage)))


単純な式のコンパイル
自己評価式、クォート、変数に対するコード生成器は必要な値をターゲットのレジスタに割当リンク記述子により指示されたように勧める命令列を構築します。
(define (compile-self-evaluating exp target linkage)
  (end-with-linkage linkage
                    (make-instruction-sequence '() (list target)
                                               `((assign ,target (const ,exp))))))
(define (compile-quoted exp target linkage)
  (end-with-linkage linkage
                    (make-instruction-sequence '() (list target)
                                               `((assign ,target (const ,(text-of-quotation exp)))))))
(define (compile-variable exp target linkage)
  (end-with-linkage linkage
                    (make-instruction-sequence '(env) (list target)
                                               `((assign ,target
                                                         (op lookup-variable-value)
                                                         (const ,exp)
                                                         (reg env))))))

これらすべての代入命令はターゲットレジスタを変更します。
また変数の検索を行うものはenvレジスタを必要とします。
代入と定義はインタプリタのものと同様に扱われます。再帰的に変数に割り当てられる値を求めるコードを生成し、それに対して実際に変数の設定、
または定義を行うものと式全体の値(シンボルOK)を割り当てるものの2つの命令列を接続します。
再帰的なコンパイルはターゲットvalとリンク記述子nextを持つのでコードはその結果をvalに入れ、その後に接続されたコードを用いて続けられる。
接続はenvを維持(preserving)している間に行われる。
環境が変数の設定、または定義のため必要なため。
また変数の値のためのコードは複雑な式のコンパイルとなり得るため任意の方法でレジスタを変更する可能性がある。

(define (compile-assignment exp target linkage)
  (let ((var (assignment-variabel exp))
        (get-value-code
         (compile (assignment-value exp) 'val 'next)))
    (end-with-linkage linkage
                      (preserving '(env)
                                  get-value-code
                                  (make-instruction-sequence '(env val) (list target)
                                                             `((perform (op set-variable-value!)
                                                                        (const ,var)
                                                                        (reg val)
                                                                        (reg env))
                                                               (assign ,target (const ok))))))))

接続された2つの命令列はenvとvalを必要とし、ターゲットを変更する。
たとえenvをこの列のために維持したとしても、valは維持しない。get-value-codeが明示的にその結果をこの命令列のためにvalに配置するように設計されているため。


条件式のコンパイル
与えられたターゲットとリンク記述子と共にコンパイルされるif式のためのコードは以下の形式を持つ。
<述語のコンパイル, ターゲットval, リンク記述子next>
(test (op false?) (reg val))
(branch (label false-branch))

true-branch
<結果部与えられたターゲット与えられたリンク記述子またはafter-if>
false-branch
<代替部与えられたターゲットリンク記述子>
after-if

このコードを生成するために、述語、結果部、代替部をコンパイルし、結果のコードを述語の結果をテストするコードと新しく生成された真と偽の分岐をマークするラベルと条件分の最後とともに組み立てる。
このコードの準備では、テストが偽である場合、真の分岐へと飛ぶ必要がある。
唯一、微妙に複雑なのは真の分岐がリンク記述子をどのように扱うかについて。
もし条件分のリンク記述子がreturnまたはラベルの場合、信徒義の分岐は両方共これと同じリンク記述子を用いる。
もしリンク記述子がnextなら真の分岐は偽の分岐を飛び越し条件分の最後へと飛ぶコードで終わる。

(define (compile-if exp target linkage)
  (let ((t-branch (make-label 'true-branch))
        (f-branch (make-label 'false-branch))
        (after-if (make-label 'after-if)))
    (let ((consequent-linkage
           (if (eq? linkage 'next) after-if linkage)))
      (let ((p-code (compile (if-predicate exp) 'val 'next))
            (c-code
             (compile
              (if-consequent exp) target consequent-linkage))
            (a-code
             (compile (if-alternative exp) target linkage)))
        (preserving '(env continue)
                    p-code
                    (append-instruction-sequences
                     (make-instruction-sequence '(val) '()
                                                `((test (op false?) (reg val))
                                                  (branch (label ,f-branch))))
                     (parallel-instruction-sequences
                      (append-instruction-sequences t-branch c-code)
                      (append-instruction-sequences f-branch a-code))
                     after-if))))))

envは述語コードの間維持される。
真と偽の分岐で必要になるかもしれないため。
そしてcontinueもそれら分岐内でリンクのためのコードにてしようされるかもしれないため維持される。
真と偽の分岐のためのコードはparallel-instruction-sequencesを使用して接続される。
condは派生式であることに注意。そのためコパイらが取り扱いのために必要なこと全てはcond-if変換器を適用して、結果のif式をコンパイルするだけ。


列のコンパイル
列のコンパイル(手続きのボディ、または明示的なbegin式)はそれらの評価を並列化する。
列の各式は次の条件でコンパイルされる。最後の式は列に対して指示されたリンク記述子を用いて。
他の式はリンク記述子nextを用いて(列の残りを実行するために)
個別の式の命令列は接続され単一の命令列を形成する。
列の残りのために必要なenvと(列の終わりのリンクコードで必要な可能性のある)continueは維持される。
(define (compile-sequence seq target linkage)
  (if (last-exp? seq)
      (compile (first-exp seq) target linkage)
      (preserving '(env continue)
                  (compile (first-exp seq) target 'next)
                  (compile-sequence (rest-exps seq) target linkage))))


lambda式のコンパイル
lambda式は手続きを構築します。lambda式のためのオブジェクトコードは以下の形式に従わなければならない。
<手続きオブジェクトの構築
 ターゲットレジスタにそれを割り当てる>
<リンク>

lambda式をコンパイルする時、手続きのボディのためのコードも生成する。
たとえボディが手続き構築時に実行されなくても、オブジェクトコードの中のlambda式のコードの直後に挿入しておくことは便利。
もしlambda式に対するリンク記述子がラベルかreturnならば、このことに問題はない。
しかし、もしリンク記述子がnextならば、手続きのボディの後ろに挿入されたラベルへ飛ぶリンク記述子を使用することによりボディに対するコードを回避する必要がある。
従ってオブジェクトコードは以下の形式になる。

<手続きオブジェクトの構築
 それをターゲットレジスタに割当>
 <与えられたリンク記述子に対するコード>または(goto (label after-lambda))
 <手続きボディのコンパイル後コード>
after-lambda

compile-lambdaは手続きのボディのコードが続く手続きオブジェクトを構築するためのコードを生成する。
手続きオブジェクトは実行時に現在の環境(定義時点での環境)をコンパイルされた手続きのボディのエントリポイント(新しく生成されたラベル)と共に組み立てることで構築される。

(define (compile-lambda exp target linkage)
  (let ((proc-entry (make-label 'entry))
        (after-lambda (make-label 'after-lambda)))
    (let ((lambda-linkage
           (if (eq? linkage 'next) after-lambda linkage)))
      (append-instruction-sequences
       (take-on-instruction-sequence
        (end-with-linkage lambda-linkage
                          (make-instruction-sequence '(env) (list target)
                                                     `((assign ,target
                                                               (op make-compiled-procedure)
                                                               (label ,proc-entry)
                                                               (reg env)))))
        (compile-lambda-body exp proc-entry))
       after-lambda))))

compile-lambdaはappend-instruction-sequencesではなく、特別な結合器take-on-instruction-sequenceを手続きのボディとlmabda式のコードを接続するのに利用する。
ボディは組み立てられた列が入力された時に実行される命令列の一部ではないため。
そうではなく、それはただ、底に置くことが便利だから、その列の中にある。
compile-lambda-bodyは手続きのボディのためのコードを構築する。
このコードはエントリポイントに対するラベルで開始する。
次に来るのは実行時の環境を手続きのボディの評価を評価するために正しい環境へとスイッチする命令列。
すなわち、手続きの定義環境であり、これは手続きが呼ばれる時に利用される引数に対する形式パラメタの束縛を含むように拡張されている。
これの後には、式の列のコードが来る。これが手続きのボディを作り上げる。
この列はリンク記述子returnとターゲットvalと共にコンパイルされるため、手続きの結果はvalに入れられた状態で手続きから戻ることで終わる。

(define (compile-lambda-body exp proc-entry)
  (let ((formals (lambda-parameters exp)))
    (append-instruction-sequences
     (make-instruction-sequence '(env proc argl) '(env)
                                `(,proc-entry
                                  (assign env
                                          (op compiled-procedure-env)
                                          (reg proc))
                                  (assign env
                                          (op extend-environment)
                                          (const ,formals)
                                          (reg argl)
                                          (reg env))))
     (compile-sequence (lambda-body exp) 'val 'return))))
